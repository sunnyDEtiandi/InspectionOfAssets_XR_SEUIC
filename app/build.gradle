apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.xiangrong.yunyang.inspectionofassets"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            // 混淆
            minifyEnabled true
            // Zipalign优化
            zipAlignEnabled true
            // 原因是Gradle2.0以上版本minifyEnabled字段为true时已经包含了shrinkResources,
            // 即在代码优化时会自动去掉多余资源,所以在打正式包时也不需要设置shrinkResources字段。
            //移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->  // each 改为 all
                    def fileName = "inventory${variant.versionName}.apk"
                    def outFile = output.outputFile
                    if (outFile != null && outFile.name.endsWith('.apk')) {
                        outputFileName = fileName  //  output.outputFile 改为 outputFileName
                    }
                    //删除unaligned apk
                    if (output.zipAlign != null) {
                        output.zipAlign.doLast {
                        }
                    }
                }
            }
        }
    }
    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    //指定jdk版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

//如果引用的第三方库的支持库版本低于（或者不一致）app build.gradle中的支持库版本，可能会出现如下问题：
//all com.android.support libraries must use the exact same version specification(mixing versions can lead to runtime crashes)
// 去改第三方库所用的支持库版本比较麻烦，如果用的库很多的话工作量很大。
// 这个时候我们可以考虑强制让所有模块都用相同的支持库版本。

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // design
    implementation 'com.android.support:design:28.0.0'
    // recyclerView
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    // QM_UI
    implementation 'com.qmuiteam:qmui:1.1.10'
    // LitePal
    implementation 'org.litepal.android:java:3.0.0'
    // ZzExcelCreator_jxl
    implementation 'com.github.zhouzhuo810:ZzExcelCreator:1.0.6'
    // ExpandableLayout
    implementation 'com.github.SilenceDut:ExpandableLayout:1.2.0'
    // EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    // BasePopup
    implementation 'com.github.razerdp:BasePopup:2.1.6'
    // UserGuideView
    implementation 'com.github.yilylong:UserGuideView:1.0.5'
    // butterknife
    implementation 'com.jakewharton:butterknife:9.0.0-rc2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc2'
    // PermissionsDispatcher
    implementation 'com.github.hotchemi:permissionsdispatcher:2.4.0'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'
    // Gson
    implementation 'com.google.code.gson:gson:2.8.5'
    // logging-interceptor
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.2'
    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    // rx+retrofit
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    // rxjava
    implementation "io.reactivex.rxjava2:rxjava:2.2.3"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
}
